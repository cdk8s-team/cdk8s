package(default_visibility = ["//visibility:public"])

load("//:packages.bzl", "CDK_PACKAGE_VERSION")
load("//tools:defaults.bzl", "jest_test", "jsii_library", "jsii_package", "package_json", "ts_library")

MODULE_NAME = "cdk8s"

# Note, we could easily make this into a macro
JSII_TARGETS = str({
    "dotnet": {
        "namespace": "Org.Cdk8s",
        "packageId": "Org.Cdk8s",
    },
    "java": {
        "maven": {
            "artifactId": "cdk8s",
            "groupId": "org.cdk8s",
        },
        "package": "org.cdk8s",
    },
    "python": {
        "distName": "cdk8s",
        "module": "cdk8s",
    },
})

package_json(
    name = "package_json",
    package_name = MODULE_NAME,
    bundled_deps = ["yaml"],
    desc = "Cloud Development Kit for Kubernetes",
    experimental = True,
    jsii_targets = JSII_TARGETS,
    peer_deps = {
        "@aws-cdk/core": CDK_PACKAGE_VERSION,
        "@aws-cdk/cx-api": CDK_PACKAGE_VERSION,
    },
    deps = {
        "yaml": "^1.7.2",
    },
)

jsii_library(
    name = "lib",
    srcs = glob(
        ["**/*.ts"],
        exclude = ["test/**"],
    ),
    module_name = MODULE_NAME,
    package_json = ":package_json",
    deps = [
        "@npm//@aws-cdk/core",
        "@npm//@types/node",
        "@npm//@types/yaml",
    ],
)

ts_library(
    name = "tests",
    testonly = True,
    srcs = glob(["test/**/*.ts"]),
    deps = [
        ":lib",
        "@npm//@aws-cdk/core",
        "@npm//@types/yaml",
        "@npm//yaml",
    ],
)

jest_test(
    name = "test",
    srcs = glob(["**/*.snap"]),
    lib = ":tests",
    deps = [
        ":lib",
        "@npm//@aws-cdk/core",
        "@npm//tslib",
        "@npm//yaml",
    ],
)

jsii_package(
    name = "pkg",
    # Note: we include @npm//yaml here because it's a bundledDependency and
    #       NPM needs it visibly in the pkg directory in order to bundle it
    #       correctly. Also note we don't bundle YAML's dependency on
    #       @babel/runtime, because we don't touch code it uses. This may
    #       change, in which case you just need to add the dependency here.
    #       (It will fail integration tests before it reaches users when that
    #       happens).
    srcs = ["README.md", "@npm//yaml"],
    package_json = ":package_json",
    tags = ["release"],
    deps = [
        ":lib",
        "@npm//@aws-cdk/core",
    ],
)
